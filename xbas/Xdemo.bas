0010 REM XBAS DEMO/TEST V0.2
0020 IF PEEK( 28677)+PEEK( 28678)*256<> 64896THEN ?"LOAD XBAS FIRST":END 
1000 REM TEXT AND BITMAP
1010 FG=2:BG=1:TXTMAP FG,BG
1020 INVERSE:PRINT "TXTMAP MODE":NORMAL
1030 LOCATE 0,2:PRINT "Characterset:"
1040 LOCATE 5,5
1050 FOR V=1TO 4:COLOR 5+V*2,BG:FOR H=0TO 31
1060 PRINT CHR$( V*32+H);
1070 NEXT H:LOCATE 5,5+V:NEXT V
1080 PLOT 30,5,FGTO 30,218TO 70,218TO 70,5TO 30,5
1090 LOCATE 0,10:COLOR FG,BG:PRINT "Plotting:"
1110 PLOT 96,5,FGTO 96,255TO 190,255TO 190,5TO 96,5
1120 PLOT 144,125
1130 FOR I=1TO 100
1140 CIRCLE 38+RND( 186),129+RND( 30),10+RND( 20),2+RND( 13)
1150 NEXT I
1500 REM FLOOD FILL
1510 CLS
1520 PLOT 88,48,FGTO 88,207TO 110,207TO 110,48TO 88,48
1530 LOCATE 15,12:PRINT "Painting a Circle"
1540 CIRCLE 128,96,90,4
1550 CIRCLE 100,48,22
1560 CIRCLE 156,48,22
1570 CIRCLE 100,144,22
1580 CIRCLE 156,144,22
1590 PAINT 128,130
1600 FOR K=1TO 1000:NEXT K
2000 REM COLORED SCROLL
2010 LOCATE 0,23:COLOR FG,BG:PRINT :PRINT :PRINT 
2020 FOR J=1TO 3:FOR I=3TO 15
2030 COLOR I,BG:IF I<9THEN PRINT SPC( I-3);ELSE PRINT SPC( 15-I);
2040 PRINT "The Quick Brown Fox Jumps Over A Lazy Dog"
2050 NEXT I:NEXT J
2060 LOCATE 0,23:COLOR FG,BG
2070 INVERSE:PRINT " The Lazy Dog Sleeps At The Bottom Of The Screen ";:NORMAL
2080 POKE 30213,23:LOCATE 0,22:PRINT 
2090 FOR J=1TO 3:FOR I=3TO 15
2100 COLOR I,BG:IF I<9THEN PRINT SPC( I-3);ELSE PRINT SPC( 15-I);
2110 PRINT "The Quick Brown Fox Continues Jumping"
2120 NEXT I:NEXT J
2130 LOCATE 0,22:COLOR FG+1,BG
2140 INVERSE:PRINT " The Quick Brown Fox Is Starting To Get Tired Now";:NORMAL
2150 POKE 30213,22:LOCATE 0,21:PRINT 
2160 FOR J=1TO 3:FOR I=3TO 15
2170 COLOR I,BG:IF I<9THEN PRINT SPC( I-3);ELSE PRINT SPC( 15-I);
2180 PRINT "Pack my box with five dozen liquor jugs"
2190 NEXT I:NEXT J
2200 FOR K=1TO 1000:NEXT K
3000 REM FAST SCROLL
3010 FG=2:BG=1:TXTMAP FG,BG:POKE 30214,2
3020 FOR I=1TO 150
3030 PRINT "Fast Scrolling Monochrome Text Mode Test ";I
3040 NEXT I
3050 LOCATE 0,23:INVERSE:PRINT "YOU CAN STILL HAVE A STATUS BAR IN THIS MODE ";:NORMAL
3060 POKE 30213,23:LOCATE 0,22:PRINT 
3070 FOR I=1TO 25
3080 PRINT "Fast Scrolling With Status Bar Test ";I
3090 NEXT I
3100 LIST 10-3100
4000 REM LOCATE AND LINPUT
4010 FG=2:BG=1:TXTMAP FG,BG
4020 BEEP
4030 PRINT "Hello, what is your name? "
4040 A$=""
4050 LINPUT 0,3,100,A$
4060 LOCATE 0,6:PRINT "Nice to meet you ";A$
4070 FOR K=1TO 1000:NEXT K
5000 REM RETURN TO NORMAL MODE
5010 EXIT
5020 CALL SCREEN(12,0)
5030 BEEP
5040 PRINT "* THAT'S ALL FOLKS *"
5050 END
