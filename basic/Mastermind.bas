0010 REM  *** MASTERMIND ***
0020 RANDOMIZE 
0030 DIM A(12,8):DIM B$(2):DIM C(4):DIM G(4)
0040 GOTO 1260
0050 REM  *** SPELREGELS ***
0060 HOME 
0070 PRINT "** MASTERMIND **  -SPELREGELS-"
0080 PRINT 
0090 PRINT "DE COMPUTER MAAKT EEN GEHEIME CODE VAN VIER VERSCHILLENDE   CIJFERS. U MOET DIE RADEN."
0100 PRINT 
0110 PRINT "DE CIJFERS LIGGEN TUSSEN 0 EN 9. HEEFT MEN EEN CIJFER GOED, MAAR NIET ZIJN PLAATS, DAN"
0120 PRINT "WORDT DIT AANGEGEVEN MET EEN  *. IS OOK DE PLAATS GOED, DAN WORDT DAT AANGEGEVEN MET EEN  #."
0130 PRINT 
0140 PRINT 
0150 PRINT "*** DRUK NU EEN TOETS IN ***"
0160 IF ASC( INKEY$ )=255THEN GOTO 160
0170 HOME 
0180 REM  * INITIALIZEREN *
0190 REM  * MATRIX A SCHOON *
0200 FOR I=1TO 12
0210 FOR J=1TO 8
0220 A(I,J)=0
0230 NEXT J
0240 NEXT I
0250 REM  * GEHEIME CODE *
0260 FOR I=1TO 4
0270 X=INT( RND( 1)*10)
0280 IF I=1THEN GOTO 380
0290 REM  ** CIJFER AL GEBRUIKT **
0300 H=0
0310 FOR J=1TO I-1
0320 IF X<> C(J)THEN GOTO 360
0330 REM  * CIJFER KOMT VOOR *
0340 H=1
0350 J=I
0360 NEXT J
0370 IF H<> 0THEN GOTO 270
0380 C(I)=X
0390 NEXT I
0400 REM  * STR.MATRIX VOL *
0410 B$(1)="*"
0420 B$(2)="#"
0430 REM  ** STARTVARIABELEN **
0440 B=0:REM  BEURT
0450 T=0:REM  CONROLETELLER
0460 A$=""
0470 RETURN 
0480 H=0
0490 REM  * UITVOER *
0500 HOME 
0510 PRINT "  *** MASTERMIND ***"
0520 PRINT "  BEURT:";B
0530 PRINT "  PLAATS EN CIJFER GOED: -#-"
0540 PRINT "  ALLEEN CIJFER GOED:    -*-"
0550 IF T=0THEN GOTO 650
0560 FOR I=1TO T
0570 PRINT A(I,1);A(I,2);A(I,3);A(I,4);" - - - - -     ";
0580 FOR J=5TO 8
0590 IF A(I,J)=0THEN GOTO 620
0600 X=A(I,J)
0610 PRINT B$(X);
0620 NEXT J
0630 PRINT 
0640 NEXT I
0650 PRINT A$;
0660 RETURN 
0670 REM  *** INVOER ***
0680 INPUT "1e CIJFER : ",G(1)
0690 INPUT "2e CIJFER : ",G(2)
0700 INPUT "3e CIJFER : ",G(3)
0710 INPUT "4e CIJFER : ",G(4)
0720 REM  ** CONTROLE OP CIJFERS**
0730 H=0
0740 FOR I=1TO 4
0750 IF (G(I)>= 0)AND (G(I)<= 9)THEN GOTO 780
0760 A$="CIJFERS INVOEREN,GEEN GETALLEN"
0770 H=1
0780 NEXT I
0790 IF H=1THEN GOTO 890
0800 FOR I=1TO 4
0810 FOR J=1TO 4
0820 IF (G(I)<> G(J))OR (I=J)THEN GOTO 870
0830 H=1
0840 I=4
0850 J=4
0860 A$="DE CIJFERS MOETEN VERSCHILLENDZIJN!"
0870 NEXT J
0880 NEXT I
0890 RETURN 
0900 REM  ** BEREKENING **
0910 T=T+1
0920 IF T<13THEN GOTO 1010
0930 REM  * CIJFERS SCHUIVEN *
0940 FOR I=1TO 11
0950 FOR J=1TO 8
0960 A(I,J)=A(I+1,J)
0970 NEXT J
0980 NEXT I
0990 T=T-1
1000 REM  * GOK OPNEMEN *
1010 FOR J=1TO 8
1020 A(T,J)=0
1030 IF J>4THEN GOTO 1050
1040 A(T,J)=G(J)
1050 NEXT J
1060 REM  * CONTROLE CIJFERS *
1070 L=4
1080 S=0
1090 FOR I=1TO 4
1100 FOR J=1TO 4
1110 IF G(I)<> C(J)THEN GOTO 1180
1120 P=1
1130 IF I<> JTHEN GOTO 1160
1140 P=2
1150 S=S+2
1160 L=L+1
1170 A(T,L)=P:REM  PLAATS/CIJFER
1180 NEXT J
1190 NEXT I
1200 IF S<> 8THEN GOTO 1230
1210 A$="U HEEFT DE CODE GERADEN "
1220 H=3
1230 RETURN 
1240 REM  * STUURMODULE *
1250 REM ***SPELREGELS***
1260 GOSUB 60
1270 GOSUB 200
1280 B=B+1
1290 GOSUB 500
1300 A$=""
1310 GOSUB 680
1320 IF H=1THEN GOTO 1280
1330 GOSUB 910
1340 IF H<3THEN GOTO 1280
1350 GOSUB 500
1360 PRINT "EN HAD IN TOTAAL ";B;" BEURTENNODIG."
1370 END 
